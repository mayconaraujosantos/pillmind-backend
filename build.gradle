plugins {
    id 'java'
    id 'application'
}

group = 'com.pillmind'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'com.pillmind.Main'
}

dependencies {
    // Dev Tools
    implementation("org.springframework.boot:spring-boot-devtools:3.1.4")

    // Web Framework
    implementation("io.javalin:javalin:6.0.0")
    implementation("org.slf4j:slf4j-api:2.0.7")
    implementation("ch.qos.logback:logback-classic:1.4.11")
    
    // OpenAPI/Swagger
    implementation("io.javalin:javalin-bundle:6.0.0")

    // Database
    implementation("org.postgresql:postgresql:42.6.0")
    implementation("org.hibernate.orm:hibernate-core:6.3.1.Final")
    implementation("com.zaxxer:HikariCP:5.0.1")
    implementation("org.flywaydb:flyway-core:9.22.0")

    // JSON
    implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2")

    // Validation
    implementation("jakarta.validation:jakarta.validation-api:3.0.2")
    implementation("org.hibernate.validator:hibernate-validator:8.0.0.Final")

    // Security
    implementation("io.jsonwebtoken:jjwt-api:0.11.5")
    implementation("io.jsonwebtoken:jjwt-impl:0.11.5")
    implementation("io.jsonwebtoken:jjwt-jackson:0.11.5")
    implementation("org.mindrot:jbcrypt:0.4")

    // Google OAuth2
    implementation("com.google.api-client:google-api-client:2.2.0")
    implementation("com.google.http-client:google-http-client-jackson2:1.43.3")

    // Caching (opcional)
    implementation("redis.clients:jedis:4.4.3")


    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("io.javalin:javalin-testtools:6.0.0")
    testImplementation("org.mockito:mockito-core:5.5.0")
    
    // H2 para testes em memória (rápido e leve)
    testImplementation("com.h2database:h2:2.2.224")
    
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'


}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs = ['-parameters']
    options.release = 21
}

tasks.named('run', JavaExec) {
    standardInput = System.in
    systemProperty 'file.encoding', 'UTF-8'
}

// Task para gerar hash BCrypt
tasks.register('generateHash', JavaExec) {
  mainClass = 'com.pillmind.Main'
  classpath = sourceSets.main.runtimeClasspath
  if (project.hasProperty('password')) {
    args = [project.property('password')]
  } else {
    args = ['password123']
  }
}

// Create fat JAR
jar {
    manifest {
        attributes(
            'Main-Class': 'com.pillmind.Main',
            'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' ')
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}
